plugins {
    id "java"
    id "java-library"
    id "com.jfrog.artifactory" version "4.31.7"
    id "maven-publish"
}

repositories {
    mavenLocal()
    mavenCentral()
}

// run gradle with "-Dsnapshot=true" to automatically append "-SNAPSHOT" to the version
version = System.getenv("VERSION") ?: "dev-SNAPSHOT"
sourceCompatibility = 11

ext {
    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
    docx4JVersion = System.getProperty("docx4j.version") ? System.getProperty("docx4j.version") : '11.5.0'
}

dependencies {
    implementation('commons-io:commons-io:2.16.1')
    implementation("org.javassist:javassist:3.30.2-GA")
    implementation("org.springframework:spring-expression:5.3.39")

    // We want docx-stamper to be usable with any docx4j version, so the dependency to docx4J
    // has to be provided by the client.
    compileOnly("org.docx4j:docx4j-JAXB-ReferenceImpl:${docx4JVersion}")

    testImplementation("org.docx4j:docx4j-JAXB-ReferenceImpl:${docx4JVersion}")
    testImplementation("junit:junit:4.13.2")
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/linkyard/docx-stamper")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java

            // groupId = 'org.wickedsource.docx-stamper'
            groupId = 'ch.linkyard.docx-stamper'
            artifactId =  'docx-stamper'
            version = project.version
            pom {
                name = 'docx-stamper'
                description = 'Template engine for .docx documents.'
                url = 'https://github.com/thombergs/docx-stamper'

                licenses {
                    license {
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "thombergs"
                        name = "Tom Hombergs"
                        email = "tom.hombergs@gmail.com"
                    }
                }
                scm {
                    url = "https://github.com/thombergs/docx-stamper"
                }
            }
        }
    }
}
