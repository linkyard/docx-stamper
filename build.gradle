plugins {
    id "java"
    id "java-library"
    id "com.jfrog.artifactory" version "4.5.4"
    id "maven-publish"
}

repositories {
    mavenLocal()
    mavenCentral()
}

// run gradle with "-Dsnapshot=true" to automatically append "-SNAPSHOT" to the version
version = '1.5.0' + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")
sourceCompatibility = 11

ext {
    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
    docx4JVersion = System.getProperty("docx4j.version") ? System.getProperty("docx4j.version") : '11.4.9'
}

dependencies {
    implementation('commons-io:commons-io:2.7')
    implementation("org.javassist:javassist:3.29.2-GA")
    implementation("org.springframework:spring-expression:5.3.28")

    // We want docx-stamper to be usable with any docx4j version, so the dependency to docx4J
    // has to be provided by the client.
    compileOnly("org.docx4j:docx4j-JAXB-ReferenceImpl:${docx4JVersion}")

    testImplementation("org.docx4j:docx4j-JAXB-ReferenceImpl:${docx4JVersion}")
    testImplementation("junit:junit:4.13.2")
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId 'org.wickedsource.docx-stamper'
            artifactId 'docx-stamper'
            version project.version
            pom {
                name = 'docx-stamper'
                description = 'Template engine for .docx documents.'
                url = 'https://github.com/thombergs/docx-stamper'

                licenses {
                    license {
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "thombergs"
                        name = "Tom Hombergs"
                        email = "tom.hombergs@gmail.com"
                    }
                }
                scm {
                    url = "https://github.com/thombergs/docx-stamper"
                }
            }
        }
    }
}
